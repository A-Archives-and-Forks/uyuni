{
    textdomain "susemanager";

    import "Directory";
    import "Hostname";
    import "FileUtils";
    import "GetInstArgs";
    import "IP";
    import "Popup";
    import "Stage";
    import "Wizard";

    map args = GetInstArgs::argmap ();

    string migration_file	= Directory::tmpdir + "/susemanager_migration";
    boolean migration		= FileUtils::Exists (migration_file);

    string invalid_pw_chars	= "\"$'!";

    map<string,string> settings	= $[
	"MANAGER_IP"		: "",
    ];
    if (migration)
    {
	settings["SYS_DB_PASS"]		= "novell";
    }
    else
    {
	settings["MANAGER_ADMIN_EMAIL"]	= "";
	settings["MANAGER_ENABLE_TFTP"]	= "y";
    }

    string env_file	= Directory::tmpdir + "/env_manager";
    if (FileUtils::Exists (env_file))
    {
	SCR::Execute (.target.remove, env_file);
    }

    // read existing values, if present
    foreach (string key, string value, settings, {
	string val	= getenv (key);
	if (val != nil && val != "")
	{
	    y2internal ("value for %1 present: %2", key, val);
	    settings[key]	= val;
	}
    });

    if (settings["MANAGER_IP"]:"" == "")
    {
	map out	= (map) SCR::Execute (.target.bash_output,
	    "ip -f inet -o addr show scope global | awk '{print $4}' | awk -F \/ '{print $1}'");
	settings["MANAGER_IP"]	= splitstring (out["stdout"]:"", "\n")[0]:"";
    }
    if (!migration && settings["MANAGER_ADMIN_EMAIL"]:"" == "")
    {
	settings["MANAGER_ADMIN_EMAIL"]	= "susemanager@" + Hostname::CurrentDomain ();
    }

    term contents = `HBox (`HSpacing (1), `VBox (
	`InputField (`id ("MANAGER_IP"), `opt (`hstretch),
	    // text entry label
	    _("&IP Address of the SUSE Manager Server"), settings["MANAGER_IP"]:""),

/*
	// text entry label
	`InputField (`id ("MANAGER_USER"), `opt (`hstretch), _("SUSE Manager Database &User"),
	    settings["MANAGER_USER"]:""),
	// text entry label
	`Password (`id ("MANAGER_PASS"), `opt (`hstretch), _("SUSE Manager Database &Password"),
	    settings["MANAGER_PASS"]:""),
	// text entry label
	`Password (`id ("MANAGER_PASS2"), `opt (`hstretch), _("R&epeat Password"),
	    settings["MANAGER_PASS"]:""),
*/

	migration ?

	`InputField (`id ("SYS_DB_PASS"), `opt (`hstretch),
	    // text entry label
	    _("&Database Administrator Password"), settings["SYS_DB_PASS"]:"")

	:

	`InputField (`id ("MANAGER_ADMIN_EMAIL"), `opt (`hstretch),
	    // text entry label
	    _("&Administrator E-mail Address"), settings["MANAGER_ADMIN_EMAIL"]:"")
	,

	`VSpacing (0.5)
    ), `HSpacing (1));


    string help_text	= "";

    // dialog caption
    Wizard::SetContents (_("SUSE Manager"), contents, help_text, args["enable_back"]:true, args["enable_next"]:true);
    UI::SetFocus (`id ("MANAGER_IP"));

    any ret	= `back;
    while (true)
    {
	ret	= UI::UserInput ();
	if (ret == `back)
	{
	    break;
	}
	if (ret == `abort && Popup::ConfirmAbort(`incomplete))
	{
	    break;
	}
	if (ret == `next)
	{
	    if (!IP::Check ((string) UI::QueryWidget (`id ("MANAGER_IP"), `Value)))
	    {
		Popup::Error (IP::Valid4 ());
		UI::SetFocus (`id ("MANAGER_IP"));
		continue;
	    }
	    string pw1	= (string) UI::QueryWidget (`id ("MANAGER_PASS"), `Value);
	    if (pw1 == "")
	    {
		Popup::Error (_("Password is missing."));
		UI::SetFocus (`id ("MANAGER_PASS"));
		continue;
	    }
	    if (pw1 != UI::QueryWidget (`id ("MANAGER_PASS2"), `Value))
	    {
		Popup::Error (_("Passwords do not match."));
		UI::SetFocus (`id ("MANAGER_PASS"));
		continue;
	    }
	    //DB Password needs to be 7 chars long at least. Invalid chars: " $ ' !
	    if (size (pw1) < 7)
	    {
		Popup::Error (sformat (_("The password should have at least %1 characters."), 7));
		UI::SetFocus (`id ("MANAGER_PASS"));
		continue;
	    }
	    if (pw1 != deletechars (pw1, invalid_pw_chars))
	    {
		Popup::Error (sformat (_("The password contains invalid characters.
The invalid characters are: %1"), invalid_pw_chars));
		UI::SetFocus (`id ("MANAGER_PASS"));
		continue;
	    }

	    if (!migration)
	    {
		string email	= (string) UI::QueryWidget (`id ("MANAGER_ADMIN_EMAIL"), `Value);
		if (!issubstring (email, "@"))
		{
		    Popup::Error (_("The Administrator E-mail Address is not valid."));
		    UI::SetFocus (`id ("MANAGER_ADMIN_EMAIL"));
		    continue;
		}
	    }

	    // now, values are considered correct
	    foreach (string key, string value, settings, {
		string val	= value;
		if (key == "MANAGER_ENABLE_TFTP")
		{
		    val	= value; // not set from UI
		}
		else
		{
		    val	= (string) UI::QueryWidget (`id (key), `Value);
		}
		setenv (key, val, true);
		// write env files
		SCR::Execute (.target.bash, sformat ("echo 'export %1=\"%2\"' >> %3", key, val, env_file));
	    });

	    break;
	}
    }

    return ret;
}
